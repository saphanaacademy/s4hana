************************
**
**  airport view
**
************************

name:		ZX##_I_AIRPORT

description:	Airport, private view, VDM interface view

@AbapCatalog.sqlViewName: 'ZX##_IAIRPORT'           
@AccessControl.authorizationCheck: #NOT_REQUIRED 
@EndUserText.label: 'Airport'  
@Analytics.dataCategory: #DIMENSION               
@Analytics.dataExtraction.enabled: true                        
@VDM.viewType: #BASIC
@ObjectModel.representativeKey: 'Airport'                               

define view ZX##_I_Airport as                 
select from sairport {
  @ObjectModel.text.element:  [ 'AirportName' ] 
  key sairport.id     as Airport,
  @Semantics.text
  sairport.name       as AirportName,
  sairport.time_zone  as AirportTimeZone                 
}


************************
**
**  airport query view
**
************************

name:		ZX##_C_AirportQuery 

description:	Airport query, public view, VDM consumption view

@AbapCatalog.sqlViewName: 'ZX##_CAIRPORTQ'
@AccessControl.authorizationCheck: #NOT_REQUIRED
@EndUserText.label: 'Airport query'
@OData.publish: true
@VDM.viewType: #CONSUMPTION

define view ZX##_C_AirportQuery as 
select from ZX##_I_Airport {
    key ZX##_I_Airport.Airport, 
    ZX##_I_Airport.AirportName,
    ZX##_I_Airport.AirportTimeZone
} 


************************
**
**  airport geo and service view
**
************************

name: ZX##_IAIRPORTGEO

desc: Airport geo and service, private view, VDM interface view

@AbapCatalog.sqlViewName: 'ZX##_IAIRPORTGEO'
@AccessControl.authorizationCheck: #NOT_REQUIRED
@EndUserText.label: 'Airport geo and service'
@Analytics.dataCategory: #DIMENSION 
@VDM.viewType: #BASIC
@ObjectModel.representativeKey: 'airportcode'

define view ZX##_I_AirportGeo as select from zv##_airprtserv {
    key zv##_airprtserv.airportcode, 
    @Semantics.geoLocation.latitude: true
    zv##_airprtserv.airportlat, 
    @Semantics.geoLocation.longitude: true
    zv##_airprtserv.airportlon, 
    @Semantics.url: true
    zv##_airprtserv.airportservice
}


**************************************************************
*
*   extend airport query view
*
**************************************************************

name:	ZX##_E_AirportQGS

descr:	Airport, geo and service, VDM extend view

* your final syntax is going to look something like this later: 

@AbapCatalog.sqlViewAppendName: 'ZX##_EAIRPORTQGS'
@EndUserText.label: 'Airport, geo and service, VDM extend view'
extend view ZX##_C_AirportQuery with ZX##_E_AirportQGS
association to ZX##_I_AirportGeo as _AirportGS
    on $projection.acx  = _AirportGS.airportcode
{
    ZX##_I_Airport.airport as acx,
    _AirportGS.airportcode, 
    _AirportGS.airportlat, 
    _AirportGS.airportlon, 
    _AirportGS.airportservice
}

